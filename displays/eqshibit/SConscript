Import('env', 'embedManifest')
eqshibitappenv = env.Copy()

eqshibitappenv.AppendUnique(CPPPATH = ['#/aqsis/aqsistypes', '#/thirdparty/tinyxml', '#/renderer/ddmanager'])
eqshibitappenv.AppendUnique(LIBS = ['$tiff_lib', '$fltk_lib','#/thirdparty/tinyxml'])
eqshibitappenv.AppendUnique(LIBPATH = ['/usr/X11R6/lib'])
eqshibitappenv.AppendUnique(CPPDEFINES=['EQSHIBIT_APP_EXPORTS', 'TIXML_USE_STL'])

eqshibitapp_sources = Split("""
	eqshibit.cpp
	eqshibitbase.cpp
	ddserver.cpp
	image.cpp
	framebuffer.cpp
	book.cpp
	resizebrowser.cpp
	displayserverimage.cpp
	eqshibit_ui.cpp""")

eqshibitapp_headers =  Split("""
	eqshibit.h
	eqshibitbase.h
	ddserver.h
	image.h
	book.h
	framebuffer.h
	resizebrowser.h
	displayserverimage.h
	eqshibit_ui.h""")

# Set any platform specific options for this target
eqshibitappenv.UseTargetOptions('eqshibitapp')

eqshibitapp = eqshibitappenv.Program('eqshibit', eqshibitapp_sources, LIBS=['aqsistypes','argparse','fltk','m','Xext','X11','tinyxml', 'boost_thread', 'pthread'])
eqshibitappenv.Install('$BINDIR', eqshibitapp)
embedManifest(env, eqshibitappenv, eqshibitapp, 'EXE')

env.Distribute(eqshibitapp_sources)
env.Distribute(eqshibitapp_headers)
env.Distribute('eqshibit_ui.fl')

# Display driver for communicating with eqshibit
eqshibitdisplayenv = env.Copy()
eqshibitdisplayenv.AppendUnique(CPPPATH=['#/renderer/ddmanager','#/thirdparty/tinyxml'])
eqshibitdisplayenv.AppendUnique(CPPDEFINES=['TIXML_USE_STL'])

eqshibitdisplay_sources = Split("""
	eqshibitdisplay.cpp
	displayhelpers.c""")

eqshibitdisplay_headers =  Split("""
	eqshibitdisplay.h""")

eqshibitdisplayenv.AppendUnique(LIBS = ['aqsistypes', 'tinyxml'])

# Set any platform specific target options.
eqshibitdisplayenv.UseTargetOptions('eqshibitdisplay')

eqshibitdisplay = eqshibitdisplayenv.SharedLibrary('eqshibit', eqshibitdisplay_sources)

embedManifest(env, eqshibitdisplayenv, eqshibitdisplay, 'DLL')

	
eqshibitdisplayenv.Install('$DISPLAYSDIR', eqshibitdisplay)

Return('eqshibitdisplay')

env.Distribute(eqshibitdisplay_sources)
env.Distribute(eqshibitdisplay_headers)

env.Distribute('SConscript')
